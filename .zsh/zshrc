#                 .__
# ________  ______|  |__ _______   ____
# \___   / /  ___/|  |  \\_  __ \_/ ___\
#  /    /  \___ \ |   Y  \|  | \/\  \___
# /_____ \/____  >|___|  /|__|    \___  >
#       \/     \/      \/             \/
# FIGMENTIZE: zshrc

# Zshrc by Izaak van Dongen/oh-my-zsh/probably some other people
# I no longer use oh-my-zsh as I found it a little too restrictive regarding
# setting some options and variables (HISTSIZE in particular), and also it
# seemed to persistently mess up my locale environment variables like LC_ALL

# define this function so that I can source things with impunity, but my zshrc
# won't break as hard if taken out of context.
# it goes through all of its arguments, stopping as soon as it can source any
# one of them.
source_if_exists() {
    for sfile; do
        if [[ -f "$sfile" ]]; then
            source "$sfile"
            return 0
        fi
    done
    >&2 echo "Izaak's zshrc: could not source any of $*"
    return 1
}

# read lines of $1 into goedel_array
function read_array() {
    goedel_array=()
    while IFS='' read -r line; do
        goedel_array+=("$line");
    done < "$1"
}

source_if_exists "$ZDOTDIR/tmuxopen.sh"
source_if_exists "$ZDOTDIR/ttysetup.sh"
source_if_exists "$ZDOTDIR/dircolors.sh"

# transparency in xterm
[[ -n "$XTERM_VERSION" ]] && transset-df --id "$WINDOWID" 0.97 >/dev/null

# provides things like the $fg_bold array
autoload -Uz colors && colors

unalias run-help
autoload -Uz run-help
alias help='run-help'

# killer feature!
# make rprompt go away when I move on. This hugely reduces clutter when you
# resize the screen a lot, as the active rprompt gets redrawn, and means you can
# easily copy/paste etc etc
setopt transient_rprompt

POWERLEVEL_THEME="$ZDOTDIR/plugins/powerlevel10k/powerlevel10k.zsh-theme"

# set up my prompt and general look of zsh.
if [[ -n "$GOEDEL_IS_TTY" ]]; then
    source "$ZDOTDIR/prompts/tty.zsh"
elif [[ -z "$GOEDEL_NO_POWERLINE" && -f "$POWERLEVEL_THEME" ]]; then
    if [[ -z "$GOEDEL_NOPUREPOWER" ]]; then
        # default pathway, when nothing is disabled
        source "$ZDOTDIR/prompts/purepower.zsh"
    else
        # disable purepower but still use powerline
        # this currently isn't aliased to anything.
        source "$ZDOTDIR/prompts/powerline.zsh"
    fi
else
    # disable powerline, no fancy business
    export GOEDEL_NO_POWERLINE=true
    source "$ZDOTDIR/prompts/vanilla.zsh"
fi

source_if_exists "$ZDOTDIR/completion.zsh"
source_if_exists "$ZDOTDIR/options.zsh"
source_if_exists "$ZDOTDIR/keys.zsh"

ZAPPARIXHOME="$ZDOTDIR/apparix"
ZAPPARIX_ACTIVE=false
source_if_exists "$ZDOTDIR/apparix.zsh"

alias -g ...='../..'
alias -g ....='../../..'
alias -g .....='../../../..'
alias -g ......='../../../../..'
alias d='dirs -v | head -10'

# zsh syntax highlighting
# IMPORTANT: This needs to go at the end of the file because it hooks into other
# ZLE widgets
source_if_exists "$ZDOTDIR/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

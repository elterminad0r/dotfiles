" FIGMENTIZE: 'vimrc'
"        .__
" ___  __|__|  _____ _______   ____
" \  \/ /|  | /     \\_  __ \_/ ___\
"  \   / |  ||  Y Y  \|  | \/\  \___
"   \_/  |__||__|_|  /|__|    \___  >
"                  \/             \/

" note to self ":set rl": this is funny, eg do:
" :autocmd BufWinEnter * set rl
" :autocmd CmdWinEnter * set rl
" similarly,
"   set columns=12: smash everything into 12 columns
"                   doesn't work so well on tiling WMs
"   set revins: you write backwards
"   silent !
" a nice combination is :set rl ri<CR>:sil !
" also, :noremap! a b| noremap! b a
" .. etc

" TODO: insert-mode mapping to close the innermost unclosed delimiter.
" TODO: text object for a filename, as in <cfile> maybe
" TODO: add mnemonic comments to mappings where appropriate

" TODO: why does zsh indent comments by a tab

" delete all previous autocmds. This is probably pure paranoia but the horrors
" I've endured...
" FIXME: clear functions, commands, mappings?
" this also breaks plugins, I think. Whoops!
" autocmd!
" mapclear

set encoding=utf-8

" leave the heathens to their folly
if v:progname =~? "evim"
    finish
endif

" Use space as the leader key. This key is used by plugins, and by you, to make
" custom longer mappings. By default it's set to be \ (backslash) which is
" frankly terrible. Space is much faster to type
map <Space> <Nop>
let mapleader = ' '
" To see what kind of mappings there are with it, issue :map <Leader>

" colour-related options, wrapped in some conditions just to be on the safe side
if &t_Co > 2 || has("gui_running")
    syntax enable
    " do not set hlsearch if it is already set, as this will annoyingly
    " re-highlight searches if you've set :noh
    if !&hlsearch
        set hlsearch
    endif
    " when I open a hacked together light terminal, use a light colorscheme.
    if $IZAAK_COLORSCHEME == "light"
        set background=light
    else
        set background=dark
    endif
endif

" Source the sub-rc files for various components
source ~/.vim/pluginrc
source ~/.vim/optionsrc
source ~/.vim/mappingsrc
source ~/.vim/commandrc

" some hacky functionality to toggle search highlighting
if !exists("g:izaak_highlighted")
    let g:izaak_highlighted = 1
endif

" noh doesn't work from inside functions, so I use feedkeys instead. This is
" probably pretty fragile or dangerous or whatever but who cares
function! ToggleHighlight()
    if &hlsearch
        if g:izaak_highlighted
            call feedkeys(":nohlsearch\<CR>")
        else
            call feedkeys(":set hlsearch\<CR>")
        endif
        let g:izaak_highlighted = xor(1, g:izaak_highlighted)
    endif
endfunction

" colour related options that have to happen after sourcing plugins
if &t_Co > 2 || has("gui_running")
    if &term != "linux"
        " if the solarized colorscheme exists, load it
        try
            let g:solarized_termtrans = 1
            " provide default colorscheme with not too objectionable
            " highlighting of cursorcolumn, cursorline, line numbers and bad
            " spellings.
            colorscheme solarized
        catch /^Vim\%((\a\+)\)\=:E185/
            colorscheme default
        endtry
    endif
    " makes it so I can see what I'm highlighting
    highlight Visual cterm=reverse ctermbg=NONE
endif

" make useful directories in ~/.vim where they don't exist

" make directory if it does not exist
function! MkDirP(dirname)
    if !isdirectory(a:dirname)
        echomsg "mkdir" a:dirname
        call mkdir(a:dirname)
    endif
endfunction

call MkDirP($HOME . "/.vim")
for dirname in split("backups bundle sessions swap spell undo view")
    call MkDirP($HOME . "/.vim/" . dirname)
endfor

" Basically, let vim load plugins for filetypes.
" This is pretty essential to any vim user's quality of life
" Technically speaking vim-plug already does this for me. however i don't have
" the presence of mind to remember to reinstate this should I grow tired of
" vim-plug, so it stays.
filetype plugin indent on

" similarly name and shame trailing whitespace
highlight ExtraWhitespace ctermbg=red guibg=red
augroup ExtraWhitespaceMatch
    autocmd! VimEnter,WinEnter * match ExtraWhitespace /\s\+$/
augroup END

" generating and using ctags
if executable("ctags")
    function! MakeTags()
        silent !echo "runnning ctags in $(pwd)"; ctags -R -o newtags; mv -f newtags tags
        redraw!
    endfunction
else
    function! MakeTags()
        echoerr "ctags executable not found"
    endfunction
endif

nnoremap <silent> <Leader>c :call MakeTags()<CR>

" workaround because you can't redefine a running function.
" This means that if this function is modified, YOU MUST EXIT VIM
if !exists("*ReloadVimConf")
    function! ReloadVimConf()
        source $MYVIMRC
        if has("gui_running")
            source $MYGVIMRC
        endif
        call plug#load(keys(g:plugs))
        redraw | echo "reloaded " . $MYVIMRC
    endfunction
endif
" reload vimrc when written
augroup ConfigReloadVim
    " the nested makes things not break - see autocmd-nested
    autocmd! BufWritePost ~/.vim/*rc nested call ReloadVimConf()
augroup END

function! ReloadTmuxConf()
    !tmux source-file %
    redraw | echo "reloaded " . @%
endfunction
augroup ConfigReloadTmux
    autocmd! BufWritePost ~/.tmux.conf call ReloadTmuxConf()
augroup END

function! ReloadXConf()
    !xrdb merge %
    redraw | echo "reloaded ". @%
endfunction
augroup ConfigReloadX
    autocmd! BufWritePost ~/.Xresources,~/.Xdefaults call ReloadXConf()
augroup END

function! ReloadI3Conf()
    !i3-msg restart
    redraw | echo "reloaded " . @%
endfunction
augroup ConfigReloadI3
    autocmd! BufWritePost ~/.config/i3/config,~/.config/i3status/config,~/.conkyrc,~/.config/i3blocks/config call ReloadI3Conf()
augroup END
